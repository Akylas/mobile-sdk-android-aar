buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.12.1' // match your Gradle version
    }
}

plugins {
    id 'de.undercouch.download' version '5.1.1'
    id 'maven-publish'
}

// Pass GitHub release URL and version as properties
def libVersion = project.findProperty("version") ?: System.getenv("VERSION") ?: ""
def aarUrl = project.findProperty("aarUrl") ?: System.getenv("AAR_URL") ?: ""

if (!aarUrl) {
    throw new GradleException("You must provide -PaarUrl=<url-to-aar>")
}
if (!libVersion) {
    throw new GradleException("You must provide -Pversion=<version>")
}
def downloadedAar = layout.buildDirectory.file("downloads/external-lib.aar")
def outputAarName = "carto-mobile-sdk-android-${libVersion}.aar"
def outputAar = layout.buildDirectory.file("output/aar/$outputAarName")

tasks.register('downloadExternalAar', de.undercouch.gradle.tasks.download.Download) {
    src aarUrl
    dest downloadedAar
    onlyIfModified true
    doLast {
        // Optionally copy to build/libs if you want
        println "Downloaded AAR: ${downloadedAar.get().asFile}, size: ${downloadedAar.get().asFile.length()} bytes"
    }
}

// tasks.register("prepareArtifact") {
//     dependsOn("downloadExternalAar")
//     doLast {
//         // Optionally copy to build/libs if you want
//         println "Downloaded AAR: ${downloadedAar.get().asFile}, size: ${downloadedAar.get().asFile.length()} bytes"
//     }
// }

publishing {
   
    publications {
        release(MavenPublication) {
            groupId = 'com.carto'
            artifactId = 'carto-mobile-sdk'
            version = libVersion

            artifact(downloadedAar) {
                builtBy(tasks.named("downloadExternalAar"))
                extension = 'aar'
            }
        }
    }

    repositories {
        maven {
            // This points to the Maven local repository that JitPack reads
            url = uri(System.getProperty("user.home") + "/.m2/repository")
        }
    }
}
